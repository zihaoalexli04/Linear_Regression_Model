Sub InsertGenderPieChartFromJColumn()
    Dim srcSheet As Worksheet
    Dim tgtSheet As Worksheet
    Dim chartObj As ChartObject
    Dim genderRange As Range
    Dim lastRow As Long
    Dim dict As Object
    Dim cell As Range
    Dim i As Integer

    Set srcSheet = Worksheets("Count of Recipient Gender")
    lastRow = srcSheet.Cells(srcSheet.Rows.Count, "J").End(xlUp).Row
    Set genderRange = srcSheet.Range("J2:J" & lastRow)
    Set dict = CreateObject("Scripting.Dictionary")
    For Each cell In genderRange
        If Len(Trim(cell.Value)) = 0 Then
            dict("Blank") = dict("Blank") + 1
        Else
            dict(cell.Value) = dict(cell.Value) + 1
        End If
    Next cell

    On Error Resume Next
    Set tgtSheet = Worksheets("GenderChartSheet")
    If tgtSheet Is Nothing Then
        Set tgtSheet = Worksheets.Add
        tgtSheet.Name = "GenderChartSheet"
    End If
    On Error GoTo 0
    tgtSheet.Cells.Clear

    i = 2
    tgtSheet.Range("A1").Value = "Gender"
    tgtSheet.Range("B1").Value = "Count"
    For Each key In dict.Keys
        tgtSheet.Cells(i, 1).Value = key
        tgtSheet.Cells(i, 2).Value = dict(key)
        i = i + 1
    Next key

    Set chartObj = tgtSheet.ChartObjects.Add(Left:=100, Top:=50, Width:=400, Height:=300)
    With chartObj.Chart
        .SetSourceData Source:=tgtSheet.Range("A1:B" & i - 1)
        .ChartType = xlPie
        .HasTitle = True
        .ChartTitle.Text = "Count of Recipient Gender"
        .Legend.Position = xlLegendPositionBottom
    End With
End Sub
